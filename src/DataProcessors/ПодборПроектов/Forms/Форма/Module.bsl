#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		ЭтаФорма.АвтоЗаголовок = Ложь;
		ЭтаФорма.Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	ДатаДокумента = Параметры.ДатаДокумента;
	Если Формат(ДатаДокумента, "ДЛФ=D") = Формат(ТекущаяДата(), "ДЛФ=D") Тогда
		Часы    = Час(ДатаДокумента);
		Минуты  = Минута(ДатаДокумента);
		Секунды = Секунда(ДатаДокумента);
		Если Часы = 0 И Минуты = 0 И Секунды = 0 Тогда
			ДатаДокумента = ТекущаяДата();
		КонецЕсли;
	КонецЕсли;
	СписокКлючейАналитики.Параметры.УстановитьЗначениеПараметра("ДатаСреза" , ДатаДокумента);
	
	Если НЕ Параметры.Контрагент = Неопределено Тогда
		СписокКлючейАналитики.ТекстЗапроса = СписокКлючейАналитики.ТекстЗапроса + " ГДЕ КлючиАналитикиУчетаПоКонтрагентам.Контрагент = &Контрагент";
		СписокКлючейАналитики.Параметры.УстановитьЗначениеПараметра("Контрагент", Параметры.Контрагент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	СтруктураВыбранныхПроектов = Новый Структура;
	СтруктураВыбранныхПроектов.Вставить("Корзина", Объект.Корзина);
	ОповеститьОВыборе(СтруктураВыбранныхПроектов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	ПеренестиВДокумент = Истина;
	Закрыть(КодВозвратаДиалога.OK);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокКлючейАналитикиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДобавитьПроектВКарзинуНаСервере(ВыбраннаяСтрока);
	
	УП_ПодборПроектовКлиентСервер.УстановитьТекстИнформационнойНадписи(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПроектВКарзинуНаСервере(ВыбраннаяСтрока)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	// проверим, а нет ли такой строки уже
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Контрагент", ВыбраннаяСтрока.Контрагент);
	ПараметрыОтбора.Вставить("Клиент"    , ВыбраннаяСтрока.Клиент);
	ПараметрыОтбора.Вставить("Проект"    , ВыбраннаяСтрока.Проект);
	НайденныеСтроки = ОбработкаОбъект.Корзина.НайтиСтроки(ПараметрыОтбора);
	Если НайденныеСтроки.Количество() > 0 Тогда
		СтрокаСообщения = "Проект: """ + Строка(ВыбраннаяСтрока.Проект) + """ клиента: """ + Строка(ВыбраннаяСтрока.Клиент) + """ уже выбран";
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрокаСообщения;
		Сообщение.Сообщить();
	Иначе
		НоваяСтрока            = ОбработкаОбъект.Корзина.Добавить();
		НоваяСтрока.Контрагент = ВыбраннаяСтрока.Контрагент;
		НоваяСтрока.Клиент     = ВыбраннаяСтрока.Клиент;
		НоваяСтрока.Проект     = ВыбраннаяСтрока.Проект;
		ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьЗавершенныеПриИзменении(Элемент)
	
	Если СписокКлючейАналитики.КомпоновщикНастроек.Настройки.Отбор.Элементы.Количество() > 0 Тогда
		ЭлементОтбора = СписокКлючейАналитики.КомпоновщикНастроек.Настройки.Отбор.Элементы[0];
		Если ПоказыватьЗакрытые Тогда
			ЭлементОтбора.Использование = Ложь;
		Иначе
			ЭлементОтбора.Использование = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УП_ПодборПроектовКлиентСервер.УстановитьТекстИнформационнойНадписи(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнформационнаяНадписьНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УП_ПодборПроектовКлиентСервер.ПриНажатииНаИнформационнуюНадпись(ЭтаФорма, Объект);
	
КонецПроцедуры

#КонецОбласти