#Область ОбработчиковСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МесяцСопровождения = НачалоМесяца(ТекущаяДата());
	ОбновитьСписокКонтрагентов();
	
КонецПроцедуры

#КонецОбласти

#Область КомандФормы

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	
	ОбновитьСписокКонтрагентов();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьДокументыНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Загружено.Выбран,
	|	Загружено.Исполнитель,
	|	Загружено.Контрагент,
	|	Загружено.Клиент
	|ПОМЕСТИТЬ ВТ_ИсходныеДанные
	|ИЗ
	|	&Загружено КАК Загружено
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИсходныеДанные.Контрагент КАК Контрагент,
	|	ВТ_ИсходныеДанные.Клиент КАК Клиент,
	|	ВТ_ИсходныеДанные.Исполнитель
	|ИЗ
	|	ВТ_ИсходныеДанные КАК ВТ_ИсходныеДанные
	|ГДЕ
	|	ВТ_ИсходныеДанные.Выбран
	|ИТОГИ ПО
	|	Контрагент,
	|	Клиент";
	Запрос.УстановитьПараметр("Загружено", СписокКонтрагентов.Выгрузить());
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ВыборкаЗапроса_Контрагент = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаЗапроса_Контрагент.Следующий() Цикл
			// создаем документ
			НовыйДокумент                = Документы.СогласованиеПроектов.СоздатьДокумент();
			НовыйДокумент.Дата           = МесяцСопровождения;
			НовыйДокумент.Контрагент     = ВыборкаЗапроса_Контрагент.Контрагент;
			НовыйДокумент.КонтактноеЛицо = УП_РаботаСДокументамиСервер.ПолучитьКонтактноеЛицоКонтрагента(ВыборкаЗапроса_Контрагент.Контрагент);
			НовыйДокумент.Ответственный  = ПараметрыСеанса.ТекущийПользователь;
			НовыйДокумент.Комментарий    = "#Создан помощником создания проектов согласования";
			
			ВыборкаЗапроса_Клиент = ВыборкаЗапроса_Контрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаЗапроса_Клиент.Следующий() Цикл
				НовыйПроект                     = НовыйДокумент.Проекты.Добавить();
				НовыйПроект.Клиент              = ВыборкаЗапроса_Клиент.Клиент;
				НовыйПроект.Проект              = СоздатьПроект(НовыйДокумент.Контрагент, ВыборкаЗапроса_Клиент.Клиент);
				НовыйПроект.ВремяВыполнения     = "00:00";
				НовыйПроект.ВыполнитьДо         = КонецМесяца(МесяцСопровождения);
				НовыйПроект.Валюта              = УП_РаботаСДокументамиСервер.ПолучитьВалютуУчета();
				ВыборкаЗапроса_ДетальныеЗаписи = ВыборкаЗапроса_Клиент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаЗапроса_ДетальныеЗаписи.Следующий() Цикл
					НовыйПроект.РуководительПроекта = ВыборкаЗапроса_ДетальныеЗаписи.Исполнитель;
					
					НоваяСтрокаИсполнителя                 = НовыйДокумент.Исполнители.Добавить();
					НоваяСтрокаИсполнителя.Клиент          = ВыборкаЗапроса_Клиент.Клиент;
					НоваяСтрокаИсполнителя.Исполнитель     = ВыборкаЗапроса_ДетальныеЗаписи.Исполнитель;
					НоваяСтрокаИсполнителя.Проект          = НовыйПроект.Проект;
					НоваяСтрокаИсполнителя.ВремяВыполнения = "00:00";
				КонецЦикла;
			КонецЦикла;
			Попытка
				НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				Сообщить(ОписаниеОшибки());
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	
	СоздатьДокументыНаСервере();
	Предупреждение("Документы успешно созданы");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчиковСобытийЭлементовФормы

&НаКлиенте
Процедура СписокКонтрагентовПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ОбновитьСписокКонтрагентов()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИСТИНА КАК Выбран,
	|	НастройкиСозданияСопровождения.Исполнитель,
	|	НастройкиСозданияСопровождения.Контрагент,
	|	НастройкиСозданияСопровождения.Клиент
	|ИЗ
	|	РегистрСведений.НастройкиСозданияСопровождения КАК НастройкиСозданияСопровождения";
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		СписокКонтрагентов.Загрузить(РезультатЗапроса.Выгрузить());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СоздатьПроект(Контрагент, Клиент)
	
	НовыеПроект                        = Справочники.Проекты.СоздатьЭлемент();
	НовыеПроект.Контрагент             = Контрагент;
	НовыеПроект.Клиент                 = Клиент;
	НовыеПроект.ОтчетДляКлиентаПоПлану = Ложь;
	НомерМесяца = Месяц(МесяцСопровождения);
	Если НомерМесяца = 1 Тогда
		ПредставлениеМесяца = "Январь";
	ИначеЕсли НомерМесяца = 2 Тогда
		ПредставлениеМесяца = "Февраль";
	ИначеЕсли НомерМесяца = 3 Тогда
		ПредставлениеМесяца = "Март";
	ИначеЕсли НомерМесяца = 4 Тогда
		ПредставлениеМесяца = "Апрель";
	ИначеЕсли НомерМесяца = 5 Тогда
		ПредставлениеМесяца = "Май";
	ИначеЕсли НомерМесяца = 6 Тогда
		ПредставлениеМесяца = "Июнь";
	ИначеЕсли НомерМесяца = 7 Тогда
		ПредставлениеМесяца = "Июль";
	ИначеЕсли НомерМесяца = 8 Тогда
		ПредставлениеМесяца = "Август";
	ИначеЕсли НомерМесяца = 9 Тогда
		ПредставлениеМесяца = "Сентябрь";
	ИначеЕсли НомерМесяца = 10 Тогда
		ПредставлениеМесяца = "Октябрь";
	ИначеЕсли НомерМесяца = 11 Тогда
		ПредставлениеМесяца = "Ноябрь";
	ИначеЕсли НомерМесяца = 12 Тогда
		ПредставлениеМесяца = "Декабрь";
	КонецЕсли;
	НовыеПроект.Наименование = Строка(Формат(НачалоМесяца(МесяцСопровождения), "ДЛФ=D")) + "_" + Строка(Клиент) + "_" + "Сопровождение " + Строка(ПредставлениеМесяца) + " " + СтрЗаменить(Год(МесяцСопровождения), Символы.НПП, "") + " г.";
	Попытка
		НовыеПроект.Записать();
		Возврат НовыеПроект.Ссылка;
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
КонецФункции

#КонецОбласти