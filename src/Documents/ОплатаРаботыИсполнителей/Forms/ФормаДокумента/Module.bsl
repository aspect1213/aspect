&НаКлиенте
Процедура ПодборПроектов(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДатаДокумента", Объект.Дата);
	ПараметрыФормы.Вставить("Контрагент"   , Неопределено);
	
	ПараметрЗаголовок = НСтр("ru = 'Подбор проектов в %Документ%'");
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрЗаголовок = СтрЗаменить(ПараметрЗаголовок, "%Документ%", Объект.Ссылка);
	Иначе
		ПараметрЗаголовок = СтрЗаменить(ПараметрЗаголовок, "%Документ%", НСтр("ru = 'поступление оплаты от контрагента'"));
	КонецЕсли;
	ПараметрыФормы.Вставить("Заголовок", ПараметрЗаголовок);
	
	ОткрытьФорму("Обработка.ПодборПроектов.Форма", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораНаСервере(ВыбранноеЗначение)
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Для каждого ТекСтрока Из ВыбранноеЗначение.Корзина Цикл
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Контрагент", ТекСтрока.Контрагент);
		ПараметрыОтбора.Вставить("Клиент"    , ТекСтрока.Клиент);
		ПараметрыОтбора.Вставить("Проект"    , ТекСтрока.Проект);
		НайденныеСтроки = ДокументОбъект.Проекты.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() = 0 Тогда
			НоваяСтрока            = ДокументОбъект.Проекты.Добавить();
			НоваяСтрока.Контрагент = ТекСтрока.Контрагент;
			НоваяСтрока.Клиент     = ТекСтрока.Клиент;
			НоваяСтрока.Проект     = ТекСтрока.Проект;
			
			ПараметрыАналитики = Новый Структура();
			ПараметрыАналитики.Вставить("Контрагент", ТекСтрока.Контрагент);
			ПараметрыАналитики.Вставить("Клиент"    , ТекСтрока.Клиент);
			ПараметрыАналитики.Вставить("Проект"    , ТекСтрока.Проект);
			АналитикаУчетаПоКонтрагентам = УП_РаботаСДокументамиСервер.ЗначениеКлючаАналитики(ПараметрыАналитики);
			НоваяСтрока.Сумма = УП_РаботаСДокументамиСервер.ПолучитьПлановуюТекущуюСтоимостьПроектаДляИсполнителя(АналитикаУчетаПоКонтрагентам, Объект.Исполнитель);
		КонецЕсли;
	КонецЦикла;
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ОбработкаВыбораНаСервере(ВыбранноеЗначение);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УП_РаботаСДокументамиСервер.Документ_ПриСозданииНаСервере(РеквизитФормыВЗначение("Объект"), ЭтотОбъект.Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УП_РаботаСДокументамиКлиент.Документ_ПриОткрытии(Объект, Элементы);
	
КонецПроцедуры