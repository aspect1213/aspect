&НаСервере
Процедура ОбработкаВыбораНаСервере(ВыбранноеЗначение)
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Для каждого ТекСтрока Из ВыбранноеЗначение.Корзина Цикл
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Клиент", ТекСтрока.Клиент);
		ПараметрыОтбора.Вставить("Проект", ТекСтрока.Проект);
		НайденныеСтроки = ДокументОбъект.Проекты.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() = 0 Тогда
			НоваяСтрока        = ДокументОбъект.Проекты.Добавить();
			НоваяСтрока.Клиент = ТекСтрока.Клиент;
			НоваяСтрока.Проект = ТекСтрока.Проект;
			НоваяСтрока.Валюта = Константы.ВалютаУчета.Получить();
		КонецЕсли;
	КонецЦикла;
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
	Для каждого ТекСтрока Из Объект.Проекты Цикл
		ПараметрыАналитики = Новый Структура();
		ПараметрыАналитики.Вставить("Контрагент", Объект.Контрагент);
		ПараметрыАналитики.Вставить("Клиент"    , ТекСтрока.Клиент);
		ПараметрыАналитики.Вставить("Проект"    , ТекСтрока.Проект);
		АналитикаУчетаПоКонтрагентам = УП_РаботаСДокументамиСервер.ЗначениеКлючаАналитики(ПараметрыАналитики);
		
		ВремяВыполнения = УП_РаботаСДокументамиСервер.ПолучитьТекущееВремяВыполнения(Объект.Дата - 1, АналитикаУчетаПоКонтрагентам);
		ТекСтрока.ВремяВыполненияТекущее = УП_ОбщегоНазначения_Сервер.ПреобразоватьЧислоВВремя_Строка(ВремяВыполнения);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостьюКолонокТЧНаСервере()
	
	Если Объект.ВидИзменения = Перечисления.ВидыИзмененийСостоянийПроектов.ВремяВыполнения Тогда
		Элементы.ПроектыВремяВыполненияТекущее.Видимость = Истина;
		Элементы.ПроектыВалюта.Видимость                 = Истина;
		Элементы.ПроектыВремяВыполнения.Видимость        = Истина;
		Элементы.ПроектыСумма.Видимость                  = Истина;
		Элементы.ПроектыСостояниеПроекта.Видимость       = Ложь;
	ИначеЕсли Объект.ВидИзменения = Перечисления.ВидыИзмененийСостоянийПроектов.ЭтапВыполнения Тогда
		Элементы.ПроектыВремяВыполненияТекущее.Видимость = Ложь;
		Элементы.ПроектыВалюта.Видимость                 = Ложь;
		Элементы.ПроектыВремяВыполнения.Видимость        = Ложь;
		Элементы.ПроектыСумма.Видимость                  = Ложь;
		Элементы.ПроектыСостояниеПроекта.Видимость       = Истина;
	КонецЕсли;
	
КонецПроцедуры

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УП_РаботаСДокументамиСервер.Документ_ПриСозданииНаСервере(РеквизитФормыВЗначение("Объект"), ЭтотОбъект.Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УП_РаботаСДокументамиКлиент.Документ_ПриОткрытии(Объект, Элементы);
	УправлениеВидимостьюКолонокТЧНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ОбработкаВыбораНаСервере(ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

&НаКлиенте
Процедура ПодборПроектов(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДатаДокумента", Объект.Дата);
	ПараметрыФормы.Вставить("Контрагент"   , Объект.Контрагент);
	
	ПараметрЗаголовок = НСтр("ru = 'Подбор проектов в %Документ%'");
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрЗаголовок = СтрЗаменить(ПараметрЗаголовок, "%Документ%", Объект.Ссылка);
	Иначе
		ПараметрЗаголовок = СтрЗаменить(ПараметрЗаголовок, "%Документ%", НСтр("ru = 'выполнение работ над проектом'"));
	КонецЕсли;
	ПараметрыФормы.Вставить("Заголовок", ПараметрЗаголовок);
	
	ОткрытьФорму("Обработка.ПодборПроектов.Форма", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектыВремяВыполненияПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Проекты.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	УП_ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(Объект, ТекущаяСтрока, СтруктураДействий);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидИзмененияПриИзменении(Элемент)
	
	УправлениеВидимостьюКолонокТЧНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектыПроектПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Проекты.ТекущиеДанные;
	
	ПараметрыАналитики = Новый Структура();
	ПараметрыАналитики.Вставить("Контрагент", Объект.Контрагент);
	ПараметрыАналитики.Вставить("Клиент"    , ТекущаяСтрока.Клиент);
	ПараметрыАналитики.Вставить("Проект"    , ТекущаяСтрока.Проект);
	АналитикаУчетаПоКонтрагентам = УП_РаботаСДокументамиСервер.ЗначениеКлючаАналитики(ПараметрыАналитики);
	
	ВремяВыполнения = УП_РаботаСДокументамиСервер.ПолучитьТекущееВремяВыполнения(Объект.Дата - 1, АналитикаУчетаПоКонтрагентам);
	ТекущаяСтрока.ВремяВыполненияТекущее = УП_ОбщегоНазначения_Сервер.ПреобразоватьЧислоВВремя_Строка(ВремяВыполнения);
	
КонецПроцедуры

#КонецОбласти