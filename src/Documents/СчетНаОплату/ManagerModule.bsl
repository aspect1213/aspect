#Область ОбластьПечати

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати                                = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати                 = "Документ.СчетНаОплату";
	КомандаПечати.Идентификатор                  = "СчетНаОплату";
	КомандаПечати.Представление                  = НСтр("ru = 'Счет на оплату'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	КомандаПечати                                = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати                 = "Документ.СчетНаОплату";
	КомандаПечати.Идентификатор                  = "СчетНаОплатуСФаксимиле";
	КомандаПечати.Представление                  = НСтр("ru = 'Счет на оплату с факсимиле'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетНаОплату") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СчетНаОплату", "СчетНаОплату", 
		Печать_СчетНаОплату(МассивОбъектов), "Документ.СчетНаОплату.СчетНаОплату");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетНаОплатуСФаксимиле") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СчетНаОплатуСФаксимиле", "СчетНаОплатуСФаксимиле", 
		Печать_СчетНаОплату(МассивОбъектов, Истина), "Документ.СчетНаОплату.СчетНаОплату");
	КонецЕсли;
	
КонецПроцедуры

Функция Печать_СчетНаОплату(МассивОбъектов, Факсимиле = Ложь)
	
	ТабДокумент = Новый ТабличныйДокумент;
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетНаОплатуПроекты.Ссылка КАК Ссылка,
	|	СчетНаОплатуПроекты.НомерСтроки КАК НомерСтроки,
	|	СчетНаОплатуПроекты.Клиент,
	|	СчетНаОплатуПроекты.Проект КАК Проект,
	|	СчетНаОплатуПроекты.ПроектПредставление,
	|	СчетНаОплатуПроекты.ВремяВыполнения,
	|	СчетНаОплатуПроекты.Цена,
	|	СчетНаОплатуПроекты.Сумма КАК Сумма
	|ИЗ
	|	Документ.СчетНаОплату.Проекты КАК СчетНаОплатуПроекты
	|ГДЕ
	|	СчетНаОплатуПроекты.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ
	|	КОЛИЧЕСТВО(Проект),
	|	СУММА(Сумма)
	|ПО
	|	Ссылка";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Результат = Запрос.Выполнить();
	Выборка_Ссылка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка_Ссылка.Следующий() Цикл
		Макет = ПолучитьМакет("ПФ_MXL_Счет");
		
		ОрганизацияПоУмолчанию = Справочники.Организации.НайтиПоРеквизиту("ПоУмолчанию", Истина);
		
		// ЗаголовокСчетаСЛоготипом
		Область_ЗаголовокСчетаСЛоготипом                                            = Макет.ПолучитьОбласть("ЗаголовокСчетаСЛоготипом");
		Область_ЗаголовокСчетаСЛоготипом.Параметры.БанкПолучателяПредставление      = ОрганизацияПоУмолчанию.Банк;
		Область_ЗаголовокСчетаСЛоготипом.Параметры.БИКБанкаПолучателя               = ОрганизацияПоУмолчанию.БИК;
		Область_ЗаголовокСчетаСЛоготипом.Параметры.СчетБанкаПолучателяПредставление = ОрганизацияПоУмолчанию.КоррСчет;
		Область_ЗаголовокСчетаСЛоготипом.Параметры.СчетПолучателяПредставление      = ОрганизацияПоУмолчанию.РасчетныйСчет;
		Область_ЗаголовокСчетаСЛоготипом.Параметры.ИННПолучателя                    = ОрганизацияПоУмолчанию.ИНН;
		Область_ЗаголовокСчетаСЛоготипом.Параметры.КПППолучателя                    = ОрганизацияПоУмолчанию.КПП;
		Область_ЗаголовокСчетаСЛоготипом.Параметры.ПредставлениеПолучателя          = ОрганизацияПоУмолчанию.Наименование;
		ТабДокумент.Вывести(Область_ЗаголовокСчетаСЛоготипом);
		
		// Заголовок
		Область_Заголовок                          = Макет.ПолучитьОбласть("Заголовок");
		НомерБезНулей = Выборка_Ссылка.Ссылка.Номер;
		Пока Найти(НомерБезНулей, "0") = 1 Цикл
			НомерБезНулей = Сред(НомерБезНулей,2);
		КонецЦикла;
		Область_Заголовок.Параметры.ТекстЗаголовка = "Счет на оплату № " + НомерБезНулей + " от " + Формат(Выборка_Ссылка.Ссылка.Дата, "ДЛФ=DD");
		ТабДокумент.Вывести(Область_Заголовок);
		
		// Поставщик
		Область_Поставщик                       = Макет.ПолучитьОбласть("Поставщик");
		Наименование = ОрганизацияПоУмолчанию.ПолноеНаименование;
		ИНН          = ", ИНН " + ОрганизацияПоУмолчанию.ИНН + "";
		Адрес        = ?(ЗначениеЗаполнено(ОрганизацияПоУмолчанию.Адрес), ", " + ОрганизацияПоУмолчанию.Адрес, "");
		Телефон      = ?(ЗначениеЗаполнено(ОрганизацияПоУмолчанию.Телефон), ", тел.: " + ОрганизацияПоУмолчанию.Телефон, "");
		Банк         = ", р/с " + ОрганизацияПоУмолчанию.РасчетныйСчет + ", в банке " + ОрганизацияПоУмолчанию.Банк + ", БИК " + ОрганизацияПоУмолчанию.БИК + ", к/с " + ОрганизацияПоУмолчанию.КоррСчет;
		ПредставлениеПоставщика = Наименование + ИНН + Адрес + Телефон;
		Область_Поставщик.Параметры.ПредставлениеПоставщика = ПредставлениеПоставщика;
		ТабДокумент.Вывести(Область_Поставщик);
		
		// Покупатель
		Область_Покупатель                                   = Макет.ПолучитьОбласть("Покупатель");
		СведенияОЮрЛице = УП_ОбщегоНазначения_Сервер.СведенияОЮрЛице(Выборка_Ссылка.Ссылка.Плательщик);
		ИНН     = ?(ЗначениеЗаполнено(СведенияОЮрЛице.ИНН), ", ИНН " + СведенияОЮрЛице.ИНН, "");
		Адрес   = ?(ЗначениеЗаполнено(СведенияОЮрЛице.Адрес), ", " + СведенияОЮрЛице.Адрес, "");
		Телефон = ?(ЗначениеЗаполнено(СведенияОЮрЛице.Телефон), ", тел.: " + СведенияОЮрЛице.Телефон, "");
		ПредставлениеПокупателя = СведенияОЮрЛице.Наименование + ИНН + Адрес + Телефон;
		Область_Покупатель.Параметры.ПредставлениеПокупателя = ПредставлениеПокупателя;
		Область_Покупатель.Параметры.Основание               = ?(ЗначениеЗаполнено(Выборка_Ссылка.Ссылка.Договор), Выборка_Ссылка.Ссылка.Договор, "Без договора");
		ТабДокумент.Вывести(Область_Покупатель);
		
		// ШапкаТаблицы
		Область_ШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ТабДокумент.Вывести(Область_ШапкаТаблицы);
		
		Выборка_Проекты = Выборка_Ссылка.Выбрать();
		Пока Выборка_Проекты.Следующий() Цикл
			Если Выборка_Ссылка.Ссылка.ПриПечатиКоличествоРавно1 Тогда
				Количество       = 1;
				ЕдиницаИзмерения = "шт";
				Цена             = Выборка_Проекты.Сумма;
			Иначе
				Количество       = Цел(УП_ОбщегоНазначения_Сервер.ПреобразоватьВремя(Выборка_Проекты.ВремяВыполнения) / 60);
				ЕдиницаИзмерения = "ч";
				Цена             = Выборка_Проекты.Цена;
			КонецЕсли;
			// Строка
			Область_Строка                            = Макет.ПолучитьОбласть("Строка");
			Область_Строка.Параметры.НомерСтроки      = Выборка_Проекты.НомерСтроки;
			Область_Строка.Параметры.Товар            = Выборка_Проекты.ПроектПредставление;
			Область_Строка.Параметры.Количество       = Количество;
			Область_Строка.Параметры.ЕдиницаИзмерения = ЕдиницаИзмерения;
			Область_Строка.Параметры.Цена             = Цена;
			Область_Строка.Параметры.Сумма            = Выборка_Проекты.Сумма;
			ТабДокумент.Вывести(Область_Строка);
		КонецЦикла;
		
		// Итого
		Область_Итого                 = Макет.ПолучитьОбласть("Итого");
		Область_Итого.Параметры.Всего = Выборка_Ссылка.Сумма;
		ТабДокумент.Вывести(Область_Итого);
		
		// ИтогоНДС
		Область_ИтогоНДС                    = Макет.ПолучитьОбласть("ИтогоНДС");
		Область_ИтогоНДС.Параметры.НДС      = "Без налога (НДС)";
		Область_ИтогоНДС.Параметры.ВсегоНДС = "-";
		ТабДокумент.Вывести(Область_ИтогоНДС);
		
		// ИтогоКОплате
		Область_ИтогоКОплате                        = Макет.ПолучитьОбласть("ИтогоКОплате");
		Область_ИтогоКОплате.Параметры.ИтогоКОплате = Выборка_Ссылка.Сумма;
		ТабДокумент.Вывести(Область_ИтогоКОплате);
		
		// СуммаПрописью
		Область_СуммаПрописью                          = Макет.ПолучитьОбласть("СуммаПрописью");
		Область_СуммаПрописью.Параметры.ИтоговаяСтрока = "Всего наименований " + Строка(Выборка_Ссылка.Проект) + ", на сумму " + Формат(Выборка_Ссылка.Сумма, "ЧЦ=15; ЧДЦ=2") + " руб.";
		Область_СуммаПрописью.Параметры.СуммаПрописью  = ЧислоПрописью(Выборка_Ссылка.Сумма, ,"рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2");
		ТабДокумент.Вывести(Область_СуммаПрописью);
		
		// ПодвалСчетаПредприниматель
		Если Факсимиле Тогда
			Область_ПодвалСчетаПредприниматель = Макет.ПолучитьОбласть("ПодвалСчетаПредприниматель_Факсимиле");
		Иначе
			Область_ПодвалСчетаПредприниматель = Макет.ПолучитьОбласть("ПодвалСчетаПредприниматель");
		КонецЕсли;
		Область_ПодвалСчетаПредприниматель.Параметры.ФИОПредпринимателя = ОрганизацияПоУмолчанию.ФИОПредпринимателя;
		ТабДокумент.Вывести(Область_ПодвалСчетаПредприниматель);
		
		ТабДокумент.ОтображатьСетку     = Ложь;
		ТабДокумент.Защита              = Ложь;
		ТабДокумент.ТолькоПросмотр      = Истина;
		ТабДокумент.ОтображатьЗаголовки = Ложь;
		ТабДокумент.АвтоМасштаб         = Истина;
		ТабДокумент.ПолеСверху = 5;
		ТабДокумент.ПолеСлева  = 5;
		ТабДокумент.ПолеСнизу  = 5;
		ТабДокумент.ПолеСправа = 5;
		ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
	КонецЦикла;
	Возврат ТабДокумент;
	
КонецФункции

#КонецОбласти