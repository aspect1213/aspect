&НаСервере
Процедура ОбработкаВыбораНаСервере(ВыбранноеЗначение)
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Для каждого ТекСтрока Из ВыбранноеЗначение.Корзина Цикл
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Клиент", ТекСтрока.Клиент);
		ПараметрыОтбора.Вставить("Проект", ТекСтрока.Проект);
		НайденныеСтроки = ДокументОбъект.Проекты.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() = 0 Тогда
			НоваяСтрока                     = ДокументОбъект.Проекты.Добавить();
			НоваяСтрока.Клиент              = ТекСтрока.Клиент;
			НоваяСтрока.Проект              = ТекСтрока.Проект;
			НоваяСтрока.ПроектПредставление = ТекСтрока.Проект;
			
			ПараметрыАналитики = Новый Структура();
			ПараметрыАналитики.Вставить("Контрагент", Объект.Контрагент);
			ПараметрыАналитики.Вставить("Клиент"    , ТекСтрока.Клиент);
			ПараметрыАналитики.Вставить("Проект"    , ТекСтрока.Проект);
			АналитикаУчетаПоКонтрагентам = УП_РаботаСДокументамиСервер.ЗначениеКлючаАналитики(ПараметрыАналитики);
			НоваяСтрока.ВремяВыполнения  = УП_ОбщегоНазначения_Сервер.ПреобразоватьЧислоВВремя_Строка(УП_РаботаСДокументамиСервер.ПолучитьТекущееВремяВыполнения(Объект.Дата, АналитикаУчетаПоКонтрагентам));
			НоваяСтрока.Сумма            = УП_РаботаСДокументамиСервер.ПолучитьТекущуюСтоимостьПроекта(Объект.Дата, АналитикаУчетаПоКонтрагентам);
			НоваяСтрока.Цена             = УП_РаботаСДокументамиСервер.ПолучитьСтоимостьЧасаРаботыКонтрагента(Объект.Дата, Объект.Контрагент);
		КонецЕсли;
	КонецЦикла;
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
КонецПроцедуры

#Область ПроцедурыСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УП_РаботаСДокументамиСервер.Документ_ПриСозданииНаСервере(РеквизитФормыВЗначение("Объект"), ЭтотОбъект.Объект);
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УП_РаботаСДокументамиКлиент.Документ_ПриОткрытии(Объект, Элементы);
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыПрочихПроцедурИФункций

&НаКлиенте
Процедура ВопросОчисткиТабличнойЧасти(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.Проекты.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОсновнойДоговорНаСервере()
	
	Объект.Договор = Объект.Контрагент.ОсновнойДоговор;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПлательщикаНаСервере()
	
	Объект.Плательщик = Справочники.Клиенты.НайтиПоРеквизиту("Основной", Истина, , Объект.Контрагент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСуммуВТабличнойЧасти()
	
	ТекущиеДанные = Элементы.Проекты.ТекущиеДанные;
	ВремяВыполненияЧисло = УП_ОбщегоНазначения_Сервер.ПреобразоватьВремя(ТекущиеДанные.ВремяВыполнения);
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ВремяВыполненияЧисло / 60;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыСобытийЭлементовФормы

&НаКлиенте
Процедура ПроектыПроектПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Проекты.ТекущиеДанные;
	ТекущиеДанные.Сумма = УП_РаботаСДокументамиСервер.ПолучитьСуммуПроекта(ЭтотОбъект.Объект.Дата, ТекущиеДанные.Проект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Для каждого ТекСтрока Из Объект.Проекты Цикл
		ПараметрыАналитики = Новый Структура();
		ПараметрыАналитики.Вставить("Контрагент", Объект.Контрагент);
		ПараметрыАналитики.Вставить("Клиент"    , ТекСтрока.Клиент);
		ПараметрыАналитики.Вставить("Проект"    , ТекСтрока.Проект);
		АналитикаУчетаПоКонтрагентам = УП_РаботаСДокументамиСервер.ЗначениеКлючаАналитики(ПараметрыАналитики);
		ТекСтрока.Сумма              = УП_РаботаСДокументамиСервер.ПолучитьТекущуюСтоимостьПроекта(Объект.Дата, АналитикаУчетаПоКонтрагентам);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ОбработкаВыбораНаСервере(ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборПроектов(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДатаДокумента", Объект.Дата);
	ПараметрыФормы.Вставить("Контрагент"   , Объект.Контрагент);
	
	ПараметрЗаголовок = НСтр("ru = 'Подбор проектов в %Документ%'");
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрЗаголовок = СтрЗаменить(ПараметрЗаголовок, "%Документ%", Объект.Ссылка);
	Иначе
		ПараметрЗаголовок = СтрЗаменить(ПараметрЗаголовок, "%Документ%", НСтр("ru = 'выполнение проектов'"));
	КонецЕсли;
	ПараметрыФормы.Вставить("Заголовок", ПараметрЗаголовок);
	
	ОткрытьФорму("Обработка.ПодборПроектов.Форма", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	Если Объект.Проекты.Количество() > 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросОчисткиТабличнойЧасти", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, "Табличная часть содержит строки. Очистить?", РежимДиалогаВопрос.ДаНет, 60);
	КонецЕсли;
	УстановитьОсновнойДоговорНаСервере();
	УстановитьПлательщикаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектыКлиентПриИзменении(Элемент)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ПроектыВремяВыполненияПриИзменении(Элемент)
	
	ПересчитатьСуммуВТабличнойЧасти();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектыЦенаПриИзменении(Элемент)
	
	ПересчитатьСуммуВТабличнойЧасти();
	
КонецПроцедуры

#КонецОбласти